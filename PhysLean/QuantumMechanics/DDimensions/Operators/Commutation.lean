/-
Copyright (c) 2026 Gregory J. Loges. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gregory J. Loges
-/
import PhysLean.Mathematics.KroneckerDelta
import PhysLean.QuantumMechanics.DDimensions.Operators.AngularMomentum
/-!

# Commutation relations

-/

namespace QuantumMechanics
noncomputable section
open Constants
open KroneckerDelta
open SchwartzMap ContinuousLinearMap

private lemma ite_cond_symm (i j : Fin d) :
    (if i = j then A else B) = (if j = i then A else B) :=
  ite_cond_congr (Eq.propIntro Eq.symm Eq.symm)

lemma leibniz_lie {d : â„•} (A B C : ğ“¢(Space d, â„‚) â†’L[â„‚] ğ“¢(Space d, â„‚)) :
    â…A âˆ˜L B, Câ† = A âˆ˜L â…B, Câ† + â…A, Câ† âˆ˜L B := by
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, comp_assoc, comp_sub, sub_comp, sub_add_sub_cancel]

lemma lie_leibniz {d : â„•} (A B C : ğ“¢(Space d, â„‚) â†’L[â„‚] ğ“¢(Space d, â„‚)) :
    â…A, B âˆ˜L Câ† = B âˆ˜L â…A, Câ† + â…A, Bâ† âˆ˜L C := by
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, comp_assoc, comp_sub, sub_comp, sub_add_sub_cancel']

lemma comp_eq_comp_add_commute (A B : ğ“¢(Space d, â„‚) â†’L[â„‚] ğ“¢(Space d, â„‚)) :
    A âˆ˜L B = B âˆ˜L A + â…A, Bâ† := by
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, add_sub_cancel]

lemma comp_eq_comp_sub_commute (A B : ğ“¢(Space d, â„‚) â†’L[â„‚] ğ“¢(Space d, â„‚)) :
    A âˆ˜L B = B âˆ˜L A - â…B, Aâ† := by
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, sub_sub_cancel]

/-
## Position / position commutators
-/

/-- Position operators commute: `[xáµ¢, xâ±¼] = 0`. -/
lemma position_commutation_position {d : â„•} (i j : Fin d) : â…ğ±[i], ğ±[j]â† = 0 := by
  dsimp only [Bracket.bracket]
  ext Ïˆ x
  simp only [coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply,
    ContinuousLinearMap.zero_apply, SchwartzMap.zero_apply, positionOperator_apply]
  ring

lemma position_comp_commute {d : â„•} (i j : Fin d) : ğ±[i] âˆ˜L ğ±[j] = ğ±[j] âˆ˜L ğ±[i] := by
  rw [â† sub_eq_zero]
  exact position_commutation_position i j

lemma position_commutation_radiusRegPow (hÎµ : 0 < Îµ) (i : Fin d) :
    â…ğ±[i], radiusRegPowOperator (d := d) Îµ sâ† = 0 := by
  dsimp only [Bracket.bracket]
  ext Ïˆ x
  simp only [coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply]
  simp only [positionOperator_apply, radiusRegPowOperator_apply hÎµ]
  simp only [Complex.real_smul, ContinuousLinearMap.zero_apply, SchwartzMap.zero_apply]
  ring

lemma position_comp_radiusRegPow_commute (hÎµ : 0 < Îµ) (i : Fin d) :
    ğ±[i] âˆ˜L ğ«[Îµ,s] = ğ«[Îµ,s] âˆ˜L ğ±[i] := by
  rw [â† sub_eq_zero]
  exact position_commutation_radiusRegPow hÎµ _

lemma radiusRegPow_commutation_radiusRegPow (hÎµ : 0 < Îµ) :
    â…radiusRegPowOperator (d := d) Îµ s, radiusRegPowOperator (d := d) Îµ tâ† = 0 := by
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, radiusRegPowOperator_comp_eq hÎµ, add_comm, sub_self]

/-
## Momentum / momentum commutators
-/

/-- Momentum operators commute: `[páµ¢, pâ±¼] = 0`. -/
lemma momentum_commutation_momentum {d : â„•} (i j : Fin d) : â…ğ©[i], ğ©[j]â† = 0 := by
  dsimp only [Bracket.bracket]
  ext Ïˆ x
  simp only [coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply,
    ContinuousLinearMap.zero_apply, SchwartzMap.zero_apply, momentumOperator_apply_fun]
  rw [Space.deriv_const_smul _ ?_, Space.deriv_const_smul _ ?_]
  Â· rw [Space.deriv_commute _ (Ïˆ.smooth _), sub_self]
  Â· exact Space.deriv_differentiable (Ïˆ.smooth _) i
  Â· exact Space.deriv_differentiable (Ïˆ.smooth _) j

lemma momentum_comp_commute {d : â„•} (i j : Fin d) : ğ©[i] âˆ˜L ğ©[j] = ğ©[j] âˆ˜L ğ©[i] := by
  rw [â† sub_eq_zero]
  exact momentum_commutation_momentum i j

lemma momentumSqr_commutation_momentum {d : â„•} (i : Fin d) :
    â…momentumOperatorSqr (d := d), ğ©[i]â† = 0 := by
  dsimp only [Bracket.bracket, momentumOperatorSqr]
  rw [Finset.mul_sum, Finset.sum_mul, â† Finset.sum_sub_distrib]
  conv_lhs =>
    enter [2, j]
    simp only [ContinuousLinearMap.mul_def]
    rw [comp_assoc]
    rw [momentum_comp_commute j i, â† comp_assoc]
    rw [momentum_comp_commute j i, comp_assoc]
    rw [sub_self]
  rw [Finset.sum_const_zero]

lemma momentumSqr_comp_momentum_commute {d : â„•} (i : Fin d) : ğ©Â² âˆ˜L ğ©[i] = ğ©[i] âˆ˜L ğ©Â² := by
  rw [â† sub_eq_zero]
  exact momentumSqr_commutation_momentum i

/-
## Position / momentum commutators
-/

/-- The canonical commutation relations: `[xáµ¢, pâ±¼] = iâ„ Î´áµ¢â±¼ğŸ™`. -/
lemma position_commutation_momentum {d : â„•} (i j : Fin d) : â…ğ±[i], ğ©[j]â† =
    (Complex.I * â„ * Î´[i,j]) â€¢ ContinuousLinearMap.id â„‚ ğ“¢(Space d, â„‚) := by
  dsimp only [Bracket.bracket, kroneckerDelta]
  ext Ïˆ x
  simp only [ContinuousLinearMap.smul_apply, SchwartzMap.smul_apply, coe_id', id_eq, smul_eq_mul,
    coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply]
  rw [positionOperator_apply, momentumOperator_apply_fun]
  rw [momentumOperator_apply, positionOperator_apply_fun]
  simp only [neg_mul, Pi.smul_apply, smul_eq_mul, mul_neg, sub_neg_eq_add]
  have h : â‡‘(smulLeftCLM â„‚ â‡‘(Space.coordCLM i) â€¢ Ïˆ) = (fun (x : Space d) â†¦ x i) â€¢ Ïˆ := by
    ext x
    rw [ContinuousLinearMap.smul_def, smulLeftCLM_apply_apply (by fun_prop), Space.coordCLM_apply]
    simp only [Space.coord_apply, Complex.real_smul, Pi.smul_apply']
  rw [h]
  rw [Space.deriv_smul (by fun_prop) (SchwartzMap.differentiableAt Ïˆ)]
  rw [Space.deriv_component, ite_cond_symm j i]
  simp only [mul_add, Complex.real_smul, ite_smul, one_smul, zero_smul, mul_ite, mul_one, mul_zero,
    ite_mul, zero_mul]
  ring

lemma momentum_comp_position_eq (i j : Fin d) : ğ©[j] âˆ˜L ğ±[i] =
    ğ±[i] âˆ˜L ğ©[j] - (Complex.I * â„ * Î´[i,j]) â€¢ ContinuousLinearMap.id â„‚ ğ“¢(Space d, â„‚) := by
  rw [â† position_commutation_momentum]
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, sub_sub_cancel]

lemma position_position_commutation_momentum {d : â„•} (i j k : Fin d) : â…ğ±[i] âˆ˜L ğ±[j], ğ©[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ğ±[j] + (Complex.I * â„ * Î´[j,k]) â€¢ ğ±[i] := by
  rw [leibniz_lie]
  rw [position_commutation_momentum, position_commutation_momentum]
  rw [ContinuousLinearMap.comp_smul, ContinuousLinearMap.smul_comp]
  rw [id_comp, comp_id]
  rw [add_comm]

lemma position_commutation_momentum_momentum {d : â„•} (i j k : Fin d) : â…ğ±[i], ğ©[j] âˆ˜L ğ©[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ğ©[j] + (Complex.I * â„ * Î´[i,j]) â€¢ ğ©[k] := by
  rw [lie_leibniz]
  rw [position_commutation_momentum, position_commutation_momentum]
  rw [ContinuousLinearMap.comp_smul, ContinuousLinearMap.smul_comp]
  rw [id_comp, comp_id]

lemma position_commutation_momentumSqr {d : â„•} (i : Fin d) : â…ğ±[i], ğ©Â²â† =
    (2 * Complex.I * â„) â€¢ ğ©[i] := by
  unfold momentumOperatorSqr
  rw [lie_sum]
  simp only [position_commutation_momentum_momentum]
  dsimp only [kroneckerDelta]
  simp only [mul_ite_zero, ite_zero_smul, Finset.sum_add_distrib, Finset.sum_ite_eq,
    Finset.mem_univ, â†“reduceIte]
  ext Ïˆ x
  simp only [ContinuousLinearMap.add_apply, coe_smul', Pi.smul_apply, SchwartzMap.add_apply,
    SchwartzMap.smul_apply, smul_eq_mul]
  ring

lemma radiusRegPow_commutation_momentum (hÎµ : 0 < Îµ) (i : Fin d) :
    â…radiusRegPowOperator (d := d) Îµ s, ğ©[i]â† = (s * Complex.I * â„) â€¢ ğ«[Îµ,s-2] âˆ˜L ğ±[i] := by
  dsimp only [Bracket.bracket]
  ext Ïˆ x
  simp only [coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply, coe_smul',
    coe_comp', Pi.smul_apply, SchwartzMap.smul_apply, smul_eq_mul]
  simp only [momentumOperator_apply, positionOperator_apply, radiusRegPowOperator_apply_fun hÎµ]

  have hne : âˆ€ x : Space d, â€–xâ€– ^ 2 + Îµ ^ 2 â‰  0 := by
    intro x
    apply ne_of_gt
    exact add_pos_of_nonneg_of_pos (sq_nonneg _) (sq_pos_of_pos hÎµ)

  have h : (fun x â†¦ (â€–xâ€– ^ 2 + Îµ ^ 2) ^ (s / 2) â€¢ Ïˆ x) =
    (fun (x : Space d) â†¦ (â€–xâ€– ^ 2 + Îµ ^ 2) ^ (s / 2)) â€¢ Ïˆ := rfl
  have h' : âˆ‚[i] (fun x â†¦ (â€–xâ€– ^ 2 + Îµ ^ 2) ^ (s / 2)) =
      fun x â†¦ s * (â€–xâ€– ^ 2 + Îµ ^ 2) ^ (s / 2 - 1) * x i := by
    trans âˆ‚[i] ((fun x â†¦ x ^ (s / 2)) âˆ˜ (fun x â†¦ â€–xâ€– ^ 2 + Îµ ^ 2))
    Â· congr
    ext x
    rw [Space.deriv_eq, fderiv_comp]
    Â· simp only [fderiv_add_const, fderiv_norm_sq_apply, comp_smul, coe_smul', coe_comp',
        coe_innerSL_apply, Pi.smul_apply, Function.comp_apply, Space.inner_basis,
        fderiv_eq_smul_deriv, smul_eq_mul, nsmul_eq_mul, Nat.cast_ofNat]
      rw [deriv_rpow_const]
      Â· simp only [deriv_id'', one_mul]
        ring
      Â· fun_prop
      Â· left
        exact hne _
    Â· exact Real.differentiableAt_rpow_const_of_ne (s / 2) (hne x)
    Â· exact Differentiable.differentiableAt (by fun_prop)

  rw [h, Space.deriv_smul]
  Â· rw [h']
    simp only [neg_mul, smul_neg, Complex.real_smul, Complex.ofReal_mul, sub_neg_eq_add]
    ring_nf
  Â· refine DifferentiableAt.rpow ?_ (by fun_prop) (hne _)
    exact Differentiable.differentiableAt (by fun_prop)
  Â· fun_prop

lemma momentum_comp_radiusRegPow_eq (hÎµ : 0 < Îµ) (i : Fin d) :
    ğ©[i] âˆ˜L ğ«[Îµ,s] = ğ«[Îµ,s] âˆ˜L ğ©[i] - (s * Complex.I * â„) â€¢ ğ«[Îµ,s-2] âˆ˜L ğ±[i] := by
  rw [â† radiusRegPow_commutation_momentum hÎµ]
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, sub_sub_cancel]

lemma radiusRegPow_commutation_momentumSqr (hÎµ : 0 < Îµ) :
    â…radiusRegPowOperator (d := d) Îµ s, momentumOperatorSqr (d := d)â† =
    (2 * s * Complex.I * â„) â€¢ ğ«[Îµ,s-2] âˆ˜L âˆ‘ i, ğ±[i] âˆ˜L ğ©[i]
    + (s * â„ ^ 2) â€¢ ((d + s - 2) â€¢ ğ«[Îµ,s-2] - (Îµ ^ 2 * (s - 2)) â€¢ ğ«[Îµ,s-4]) := by
  unfold momentumOperatorSqr
  rw [lie_sum]
  conv_lhs =>
    enter [2, i]
    rw [lie_leibniz, radiusRegPow_commutation_momentum hÎµ]
    rw [comp_smul, â† comp_assoc, momentum_comp_radiusRegPow_eq hÎµ]
    rw [sub_comp, comp_assoc, momentum_comp_position_eq]
    simp only [kroneckerDelta, â†“reduceIte, mul_one]
  simp only [smul_comp, comp_sub, comp_smul, comp_id, smul_sub, comp_assoc,
    Finset.sum_add_distrib, Finset.sum_sub_distrib, â† Finset.smul_sum, Finset.sum_const,
    Finset.card_univ, Fintype.card_fin, â† ContinuousLinearMap.comp_finset_sum]
  rw [positionOperatorSqr_eq hÎµ, comp_sub, radiusRegPowOperator_comp_eq hÎµ, comp_smul, comp_id]
  rw [â† Nat.cast_smul_eq_nsmul â„‚]
  ext Ïˆ x
  simp only [Complex.ofReal_sub, Complex.ofReal_ofNat, sub_add_cancel, coe_sub', Pi.sub_apply,
    ContinuousLinearMap.add_apply, coe_smul', coe_comp', coe_sum', Pi.smul_apply,
    Function.comp_apply, Finset.sum_apply, map_sum, SchwartzMap.sub_apply, SchwartzMap.add_apply,
    SchwartzMap.smul_apply, SchwartzMap.sum_apply, smul_eq_mul, Complex.real_smul,
    Complex.ofReal_pow, Complex.ofReal_add, Complex.ofReal_natCast, Complex.ofReal_mul]
  ring_nf
  rw [Complex.I_sq]
  ring

/-
## Angular momentum / position commutators
-/

lemma angularMomentum_commutation_position {d : â„•} (i j k : Fin d) : â…ğ‹[i,j], ğ±[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ğ±[j] - (Complex.I * â„ * Î´[j,k]) â€¢ ğ±[i] := by
  unfold angularMomentumOperator
  rw [sub_lie]
  rw [leibniz_lie, leibniz_lie]
  rw [position_commutation_position, position_commutation_position]
  rw [â† lie_skew, position_commutation_momentum]
  rw [â† lie_skew, position_commutation_momentum]
  rw [kroneckerDelta_symm k i, kroneckerDelta_symm k j]
  simp only [ContinuousLinearMap.comp_neg, ContinuousLinearMap.comp_smul, comp_id, zero_comp,
    add_zero, add_comm, sub_neg_eq_add, â† sub_eq_add_neg]

lemma angularMomentum_commutation_radiusRegPow (hÎµ : 0 < Îµ) (i j : Fin d) :
    â…ğ‹[i,j], radiusRegPowOperator (d := d) Îµ sâ† = 0 := by
  dsimp only [Bracket.bracket]
  unfold angularMomentumOperator
  simp only [sub_mul, ContinuousLinearMap.mul_def, ContinuousLinearMap.comp_assoc]
  repeat rw [momentum_comp_radiusRegPow_eq hÎµ]
  simp only [comp_sub, comp_smulâ‚›â‚—, RingHom.id_apply, â† ContinuousLinearMap.comp_assoc]
  repeat rw [position_comp_radiusRegPow_commute hÎµ]
  simp only [ContinuousLinearMap.comp_assoc]
  rw [position_comp_commute]
  simp only [sub_sub_sub_cancel_right, sub_self]

lemma angularMomentumSqr_commutation_radiusRegPow (hÎµ : 0 < Îµ) :
    â…angularMomentumOperatorSqr (d := d), radiusRegPowOperator (d := d) Îµ sâ† = 0 := by
  unfold angularMomentumOperatorSqr
  simp only [sum_lie, smul_lie, leibniz_lie, angularMomentum_commutation_radiusRegPow hÎµ,
    comp_zero, zero_comp, add_zero, smul_zero, Finset.sum_const_zero]

/-
## Angular momentum / momentum commutators
-/

lemma angularMomentum_commutation_momentum {d : â„•} (i j k : Fin d) : â…ğ‹[i,j], ğ©[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ğ©[j] - (Complex.I * â„ * Î´[j,k]) â€¢ ğ©[i] := by
  unfold angularMomentumOperator
  rw [sub_lie]
  rw [leibniz_lie, leibniz_lie]
  rw [momentum_commutation_momentum, momentum_commutation_momentum]
  rw [position_commutation_momentum, position_commutation_momentum]
  simp only [ContinuousLinearMap.smul_comp, id_comp, comp_zero, zero_add]

lemma momentum_comp_angularMomentum_eq {d : â„•} (i j k : Fin d) : ğ©[k] âˆ˜L ğ‹[i,j] =
    ğ‹[i,j] âˆ˜L ğ©[k] - (Complex.I * â„ * Î´[i,k]) â€¢ ğ©[j] + (Complex.I * â„ * Î´[j,k]) â€¢ ğ©[i] := by
  rw [â† sub_eq_zero, sub_add]
  rw [â† angularMomentum_commutation_momentum]
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, sub_sub_cancel, sub_eq_zero]

lemma angularMomentum_commutation_momentumSqr {d : â„•} (i j : Fin d) :
    â…ğ‹[i,j], momentumOperatorSqr (d := d)â† = 0 := by
  unfold momentumOperatorSqr
  conv_lhs =>
    rw [lie_sum]
    enter [2, k]
    rw [lie_leibniz, angularMomentum_commutation_momentum]
    simp only [comp_sub, comp_smulâ‚›â‚—, RingHom.id_apply, sub_comp, smul_comp]
    rw [momentum_comp_commute _ i, momentum_comp_commute j _]
  dsimp only [kroneckerDelta]
  simp only [Finset.sum_add_distrib, Finset.sum_sub_distrib, mul_ite, mul_zero, ite_smul,
    zero_smul, Finset.sum_ite_eq, Finset.mem_univ, â†“reduceIte, sub_self, add_zero]

lemma momentumSqr_comp_angularMomentum_commute {d : â„•} (i j : Fin d) :
    ğ©Â² âˆ˜L ğ‹[i,j] = ğ‹[i,j] âˆ˜L ğ©Â² := by
  apply Eq.symm
  rw [â† sub_eq_zero]
  exact angularMomentum_commutation_momentumSqr i j

lemma angularMomentumSqr_commutation_momentumSqr {d : â„•} :
    â…angularMomentumOperatorSqr (d := d), momentumOperatorSqr (d := d)â† = 0 := by
  unfold angularMomentumOperatorSqr
  simp only [smul_lie, sum_lie, leibniz_lie]
  simp [angularMomentum_commutation_momentumSqr]

/-
## Angular momentum / angular momentum commutators
-/

lemma angularMomentum_commutation_angularMomentum {d : â„•} (i j k l : Fin d) : â…ğ‹[i,j], ğ‹[k,l]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ğ‹[j,l] - (Complex.I * â„ * Î´[i,l]) â€¢ ğ‹[j,k]
    - (Complex.I * â„ * Î´[j,k]) â€¢ ğ‹[i,l] + (Complex.I * â„ * Î´[j,l]) â€¢ ğ‹[i,k] := by
  nth_rw 2 [angularMomentumOperator]
  rw [lie_sub]
  rw [lie_leibniz, lie_leibniz]
  rw [angularMomentum_commutation_momentum, angularMomentum_commutation_position]
  rw [angularMomentum_commutation_momentum, angularMomentum_commutation_position]
  dsimp only [angularMomentumOperator, kroneckerDelta]
  simp only [ContinuousLinearMap.comp_sub, ContinuousLinearMap.sub_comp,
    ContinuousLinearMap.comp_smul, ContinuousLinearMap.smul_comp]
  ext Ïˆ x
  simp only [mul_ite, mul_one, mul_zero, ite_smul, zero_smul, coe_sub', Pi.sub_apply,
    ContinuousLinearMap.add_apply, SchwartzMap.sub_apply, SchwartzMap.add_apply, smul_sub]
  ring

lemma angularMomentumSqr_commutation_angularMomentum {d : â„•} (i j : Fin d) :
    â…angularMomentumOperatorSqr (d := d), ğ‹[i,j]â† = 0 := by
  unfold angularMomentumOperatorSqr
  conv_lhs =>
    simp only [smul_lie, sum_lie, leibniz_lie, angularMomentum_commutation_angularMomentum]
  dsimp only [kroneckerDelta]
  simp only [comp_add, comp_sub, add_comp, sub_comp, comp_smul, smul_comp, mul_ite, mul_zero,
    mul_one]
  simp only [ite_smul, zero_smul]

  -- Split into individual terms to do one of the sums, then recombine
  simp only [Finset.sum_add_distrib, Finset.sum_sub_distrib, Finset.sum_ite_irrel,
    Finset.sum_const_zero, Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte]
  simp only [â† Finset.sum_add_distrib, â† Finset.sum_sub_distrib]

  ext Ïˆ x
  simp only [angularMomentumOperator_antisymm _ i, angularMomentumOperator_antisymm j _,
    neg_comp, comp_neg, neg_neg, smul_neg, sub_neg_eq_add]
  simp only [ContinuousLinearMap.sum_apply, ContinuousLinearMap.add_apply,
    ContinuousLinearMap.sub_apply, ContinuousLinearMap.smul_apply, ContinuousLinearMap.comp_apply,
    ContinuousLinearMap.neg_apply, ContinuousLinearMap.zero_apply, SchwartzMap.add_apply,
    SchwartzMap.sum_apply, SchwartzMap.sub_apply, SchwartzMap.smul_apply, SchwartzMap.neg_apply,
    SchwartzMap.zero_apply]
  ring_nf
  rw [Finset.sum_const_zero, smul_zero]

end
end QuantumMechanics
